terraform {
  required_version = ">= 1.1, < 1.2"
}

variable "aws_region" {
  description = "The AWS region, e.g., us-east-1."
  type        = string
}

variable "stage" {
  description = "Development stage, e.g., prod."
  type        = string
}

variable "namespace" {
  description = "The namespace of the app, e.g., medicapt"
  type        = string
}

# Images and form definitions are stored in this S3 bucket as in:
# <country-code>/<form-code>
# For example: KE/ke-moh-363-2019

resource "aws_s3_bucket" "forms" {
  bucket = "${var.namespace}-${var.stage}-forms"
  acl    = "private"
  versioning {
    enabled = true
  }
  server_side_encryption_configuration {
    rule {
      apply_server_side_encryption_by_default {
        sse_algorithm     = "AES256"
      }
    }
 }
}

resource "aws_s3_bucket_public_access_block" "forms" {
  bucket = resource.aws_s3_bucket.forms.id
  block_public_acls   = true
  block_public_policy = true
  restrict_public_buckets = true
}

module "template_files" {
  source = "hashicorp/dir/template"
  version = "1.0.2"
  base_dir = "${path.module}/default-forms"
  template_vars = {
  }
}

resource "aws_s3_bucket_object" "static_files" {
  #checkov:skip=CKV_AWS_186:Letting AWS manage keys is safer here
  for_each     = module.template_files.files
  key          = each.key
  content_type = each.value.content_type
  source       = each.value.source_path
  content      = each.value.content
  etag         = each.value.digests.md5
  #
  bucket       = aws_s3_bucket.forms.id
}

# Information to locate forms is stored in dynamo
# All attributes are strings
# If a key is not required, it can be the empty string
#
# Attribute                        | Description                                                                                | Required
# ------------------------------------------------------------------------------------------------------------------------------------
# country                          | The two letter ISO country code                                                            | Y
# formUUID                         | A random v4 UUID                                                                           | Y
# locationUUID                     | Some country-internal location, whatever you want                                          | N
# lanugage                         | Two letter lanugage code ISO 639-1 lanugage codes                                          | Y
# name                             | The name of the form that providers will see                                               | Y
# subtitle                         | The subtitle, can be empty                                                                 | N
# dateEntered                      | When the form was entered into the system                                                  | Y
# dateCreated                      | When the form was officially created                                                       | Y
# formId                           | The official ID of the form if it exists                                                   | N
# tag                              | A tag to help organize forms, tags are always in English and will be translated            | Y
# priority                         | A number, highest priority is 1, within the same priority forms will be randomly organized | Y
# version                          | Version numbers start by 1, increment by an integer                                        | Y
# enabled                          | A number, 1 is enabled, 0 is diabled                                                       | Y
# country_locationUUID             | A composite key like KE! for a form in KE, no location                                     | Y - autogenerated
# country_locationUUID_enabled     | A composite key like KE!!1 for a form in KE, no location, that is enabled                  | Y - autogenerated
# country_locationUUID_enabled_tag | A composite key like KE!!1!sexual-assault for a form in KE, no location, that is enabled   | Y - autogenerated
#
# NB: locationUUID can be empty for forms which are contry-wide
#
#
# Primary key:   country
# Secondary key: formUUID
#
# Global secondary indices:
#       (when attributes aren't specified, all attributes are kept)
#       (key1 and key2 are the hash and sort keys)
#
#   1. name       : AllActiveTagsByLocation
#      key1       : country_locationUUID_enabled
#      attributes : Tag
#   2. name       : AllLocationsByCountry
#      key1       : country
#      attributes : locationUUID
#   3. name       : AllLanguagesByCountry
#      key1       : country
#      attributes : language
#   4. name       : ByUUID
#      key1       : formUUID
#   5. name       : PriorityForms
#      key1       : country_locationUUID
#      key2       : priority
#   6. name       : PriorityEnabledForms
#      key1       : country_locationUUID_enabled
#      key2       : priority
#   7. name       : PriorityEnabledFormsLanguage
#      key1       : country_locationUUID_enabled_language
#      key2       : priority
#   8. name       : PriorityEnabledFormsByTag
#      key1       : country_locationUUID_enabled_tag
#      key2       : priority
#   9. name       : PriorityEnabledFormsByTagLanguage
#      key1       : country_locationUUID_enabled_tag_language
#      key2       : priority
#
# Queries supported by the above:
#  1. Look up information on a form
#  2. Get all tags, languages, and locations for your country
#  3. Get the detials for one form by uuid, supported by ByUUID
#  4. Get the most important forms for your location
#  5. Get the most important forms for your location by tag and/or language
#
# NB The separator used in attribute names is underscore
#    The separator used in the attribute contents is !
#    So field country_enabled would store data sa CA!1
# This is due to a bug in terraform which leads to errors with various characters in attribute names
# Although, the AWS documentation also encourges this naming scheme
#
# NB ! is forbidden in any value of any field

module "dynamodb_table" {
  source = "cloudposse/dynamodb/aws" 
  # Cloud Posse recommends pinning every module to a specific version
  version     = "0.29.4"
  name                         = "${var.namespace}-${var.stage}-forms"
  enable_encryption	       = true
  enable_streams	       = false
  ttl_enabled	               = false
  # Once Medicapt scales sufficiently, it will be cheaper to configure autoscaling
  enable_autoscaler            = false
  billing_mode                 = "PAY_PER_REQUEST"
  autoscale_min_read_capacity  = null
  autoscale_min_write_capacity = null
  #
  hash_key                     = "country"
  hash_key_type	               = "S"
  range_key                    = "formUUID"
  range_key_type               = "S"
  dynamodb_attributes = [
    #
    # Fields here are commented out because terraform only wants the fields
    # which serve as indexes. But for the purposes of documentation, we list all
    # fields we intend to use.
    #
    { name = "country"
      type = "S"
    },
    { name = "formUUID"
      type ="S"
    },
    # {
    #   name = "locationUUID"
    #   type = "S"
    # },
    # {
    #   name = "language"
    #   type = "S"
    # },
    # {
    #   name = "name"
    #   type = "S"
    # },
    # {
    #   name = "subtitle"
    #   type = "S"
    # },
    # {
    #   name = "dateEntered"
    #   type = "S"
    # },
    # {
    #   name = "dateCreated"
    #   type = "S"
    # },
    # {
    #   name = "formId"
    #   type = "S"
    # },
    # {
    #   name = "tag"
    #   type = "S"
    # },
    {
      name = "priority"
      type = "S"
    },
    # {
    #   name = "version"
    #   type = "S"
    # },
    # {
    #   name = "enabled"
    #   type = "S"
    # },
    {
      name = "country_locationUUID"
      type = "S"
    },
    {
      name = "country_locationUUID_enabled"
      type = "S"
    },
    {
      name = "country_locationUUID_enabled_language"
      type = "S"
    },
    {
      name = "country_locationUUID_enabled_tag"
      type = "S"
    },
    {
      name = "country_locationUUID_enabled_tag_language"
      type = "S"
    }
  ]
  global_secondary_index_map = [
    {
      name               = "AllActiveTagsByLocation"
      hash_key           = "country_locationUUID_enabled"
      range_key          = null
      projection_type    = "INCLUDE"
      non_key_attributes = ["Tag"]
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "AllLocationsByCountry"
      hash_key           = "country"
      range_key          = null
      projection_type    = "INCLUDE"
      non_key_attributes = ["locationUUID"]
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "AllLanguagesByCountry"
      hash_key           = "country"
      range_key          = null
      projection_type    = "INCLUDE"
      non_key_attributes = ["language"]
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "ByUUID"
      hash_key           = "formUUID"
      range_key          = null
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "PriorityForms"
      hash_key           = "country_locationUUID"
      range_key          = "priority"
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "PriorityEnabledForms"
      hash_key           = "country_locationUUID_enabled"
      range_key          = "priority"
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "PriorityEnabledFormsLanguage"
      hash_key           = "country_locationUUID_enabled_language"
      range_key          = "priority"
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "PriorityEnabledFormsByTag"
      hash_key           = "country_locationUUID_enabled_tag"
      range_key          = "priority"
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    },
    {
      name               = "PriorityEnabledFormsByTagLanguage"
      hash_key           = "country_locationUUID_enabled_tag_language"
      range_key          = "priority"
      projection_type    = "ALL"
      non_key_attributes = null
      #
      read_capacity      = null
      write_capacity     = null
    }
  ]
}

resource "aws_dynamodb_table_item" "example" {
  table_name = module.dynamodb_table.table_name
  hash_key   = "country"
  range_key  = "formUUID"
  item = <<ITEM
{
   "country": { "S": "KE" },
   "formUUID": { "S": "617a8795-229c-4aa2-bdb1-e4b8fb344e32" },
   "locationUUID": { "S": "" },
   "name": { "S": "Post-rape care form" },
   "subtitle": { "S": "Keyna MOH 363" },
   "dateEntered": { "S": "2019-01-01" },
   "dateCreated": { "S": "2019-01-01" },
   "formId": { "S": "Keyna MOH 363" },
   "tag": { "S": "sexual-assault" },
   "priority": { "S": "1" },
   "version": { "S": "1" },
   "enabled": { "S": "1" },
   "country_locationUUID": { "S": "KE!" },
   "country_locationUUID_enabled": { "S": "KE!!1" },
   "country_locationUUID_enabled_tag": { "S": "KE!!1!sexual-assault" }
}
ITEM
}

output "forms_s3_bucket" {
  value = resource.aws_s3_bucket.forms
}

output "forms_dynamodb_table" {
  value = module.dynamodb_table
}
